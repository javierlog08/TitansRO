//===== eAthena Script ============================================
//= King of Emperium Hill
//===== By: =======================================================
//= AnnieRuru
//===== Current Version: ==========================================
//= 1.0
//===== Compatible With: ==========================================
//= hercules 2014-02-18
//===== Description: ==============================================
//= defends the emperium in the middle of the map until times up
//===== Topic =====================================================
//= http://hercules.ws/board/topic/4495-gvg-king-of-emperium-hill/
//===== Additional Comments: ======================================
//= Finally there is a topic for this !
//=================================================================

-	script	KoE	-1,{
OnInit:
	disablenpc "The King#KoE";
	disablenpc "KoE Manager#KoE";
	bindatcmd "koe", strnpcinfo(0)+"::OnCommand", 99,100;
	set .Reward,10; //Pts Recompenza
	end;
OnCommand:
	if ( compare( .@atcmd_parameters$, "on" ) ) goto L_Start;
	else if ( compare( .@atcmd_parameters$, "off" ) ) goto L_End;
	else {
		dispbottom "Escribe - '@koe on' Para iniciar el evento.";
		dispbottom "Escribe - '@koe off' Para finalizar el evento";
	}
	end;

OnStart:
	addtimer getvariableofnpc(.time,"pvp_koe_officer"),strnpcinfo(3)+"::OnStop";
	goto L_Start;
end;

OnStop:
	addtimer 100,"pvp_koe_officer::OnDelay";
	goto L_End;
end;

//OnClock2000: // everyday 8pm starts
L_Start:
	if ( .start ) end;
	gvgon "guild_vs1";
	announce "El PVP King Of Emperium ha sido activado !", bc_all;
	.start = 1;
	enablenpc "The King#KoE";
	disablenpc "KoE Manager#KoE";
	$koegid = 0;
	donpcevent "::OnRevKoE";
	maprespawnguildid "guild_vs1", $koegid, 7;
	monster "guild_vs1",49,49,"EMPERIUM",1288,1,"KoE::OnEmpDead";
	end;
//OnClock2030: // everyday 8:30pm ends
L_End:
	gvgoff "guild_vs1";
	announce "El PVP King Of Emperium se ha desactivado!", bc_all;
	.start = 0;
	enablenpc "KoE Manager#KoE";
	disablenpc "The King#KoE";
	killmonsterall "guild_vs1";
	maprespawnguildid "guild_vs1", $koegid, 6; // uncomment this line to kick non-owner off the map when event ends
	end;
OnEmpDead:
	$koegid = getcharid(2);
	announce "La guild ["+ strcharinfo(2) +"] Tiene ahora el control del Emperium en el PVP King Of Emperium.", bc_all;
	donpcevent "::OnRevKoE";
	maprespawnguildid "guild_vs1", $koegid, 6;
	sleep 500;
	if ( .start )
		monster "guild_vs1",49,49,"EMPERIUM",1288,1,"KoE::OnEmpDead";
	end;
}

// KoE Entrance
prontera,155,191,4	script	The King#KoE	58,{
    mes "[The King]";
	if ( !getcharid(2) ) {
		mes "Debes tener una guild para participar en el Torneo ^FF0000King of Emperium^000000.";
		close;
	}
    mes "Hola.";
    mes "Te gustaria particiar en el torneo ^FF0000King of Emperium Hill^000000?";
	if ( select ( "Si", "No" ) == 2 ) close;
    switch( rand(1,4) ){
        case 1:	warp "guild_vs1", 50, 88; end;
        case 2:	warp "guild_vs1", 88, 50; end;
        case 3:	warp "guild_vs1", 50, 11; end;
        case 4:	warp "guild_vs1", 11, 50; end;
	}
}

// KoE Exit
guild_vs1,49,49,5	script	KoE Manager#KoE	811,{
	if ( getcharid(2) == $koegid ){ 
		mes "^990099[KoE Manager]^000000";
		mes "Enohrabuena !";
		mes "Tu Guild ha demostrado ser la mejor.";
		mes "Recibe de parte de el torneo KoE ";
		mes "^808080~ "+getvariableofnpc(.Reward,"KoE")+" puntos de PVP^000000 ~";
		close2;
		set #PVPPOINTS,#PVPPOINTS+getvariableofnpc(.Reward,"KoE");
		warp "pvp_castle",113,110;
	}
	end;
}

// Flags
guild_vs1,49,38,4	script	King of Emperium Hill#1::koe_flag	722,{
	if ( !$koegid ) end;
	mes "^0066cc[King of Emperium Hill]^000000";
	mes " ";
	mes "Los campeones actuales de la KoE son la guid ^0000ff["+ getguildname($koegid) +"]^000000.";
	close;
//OnInit: // Uncomment this line to make the emblem stay after @reloadscript
OnRevKoE:
	flagemblem $koegid;
	end;
}
guild_vs1,61,49,6	duplicate(koe_flag)	King of Emperium Hill#2	722
guild_vs1,38,49,2	duplicate(koe_flag)	King of Emperium Hill#3	722
guild_vs1,49,61,0	duplicate(koe_flag)	King of Emperium Hill#4	722
//Duplicados para castillo titans ro
pvp_castle,90,79,5	duplicate(koe_flag)	King of Emperium Hill#5	722
pvp_castle,90,87,5	duplicate(koe_flag)	King of Emperium Hill#6	722
pvp_castle,90,95,5	duplicate(koe_flag)	King of Emperium Hill#7	722
pvp_castle,109,79,3	duplicate(koe_flag)	King of Emperium Hill#8	722
pvp_castle,109,87,3	duplicate(koe_flag)	King of Emperium Hill#9	722
pvp_castle,109,95,3	duplicate(koe_flag)	King of Emperium Hill#10	722

guild_vs1	mapflag	nobranch
guild_vs1	mapflag	nomemo
guild_vs1	mapflag	nopenalty
guild_vs1	mapflag	noreturn
guild_vs1	mapflag	nosave	SavePoint
guild_vs1	mapflag	noteleport
guild_vs1	mapflag	gvg_noparty
guild_vs1	mapflag	nowarp
guild_vs1	mapflag	nowarpto
guild_vs1	mapflag	guildlock