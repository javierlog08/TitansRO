//===== Hercules Script ======================================
// BattleGround System - KvM Item Dealer
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.0
//===== Description: =========================================
//= [Official Conversion]
//= Kreiger Von Midgard Battleground Point redemption NPC
//= * NOTE * 
//= KvM Does NOT use items, but a character variable to
//= keep track of points. This NPC when talked to will
//= convert already-given tokens and convert them to points.
//= under the player variable kvm_point.
//===== Additional Comments: =================================
//= 1.0 First Version.
//============================================================

pvp_castle,88,101,5	script	KVM Logistic Officer#a	4_M_KNIGHT_GOLD,{
	if (countitem(7773)) {
		set .@pointstoadd,countitem(7773);
		delitem 7773,.@pointstoadd;
		set kvm_point,kvm_point+.@pointstoadd;
		mes "[Logistics]";
		mes "Son aquellos "+getitemname(7773)+"s Que veo?";
		mes "Nosotros ya no aceptamos esa moneda,";
		mes "pero puedo cambiarla para ti,";
		mes "Asi que tu posees "+.@pointstoadd+" "+getitemname(7773)+"s?";
		mes "De acuerdo ahora tu posees ^580080"+ kvm_point +"^000000 KVM Points.";
		next;
	}
	if (checkweight(1201,1) == 0) {
		mes "- Espera un minuto !! -";
		mes "- Ahora mismo estas cargando -";
		mes "- demasiados items. -";
		mes "- Porfavor intentalo de nuevo -";
		mes "- despues de perder algo de peso. -";
		close;
	}
	mes "[Logistics]";
	mes "Hola?";
	mes "Estoy a cargo de la distribucion";
	mes "items de recompensa por puntos KVM.";
	mes "Cualquier Mala seleccion de items, no podra";
	mes "ser revertida. Porfavor tenga cuidado.";
	mes "Selecciona el proximo paso, porfavor.";
	next;
	set .@name$,strcharinfo(0);
	switch(select("Leer el catalogo de KVM.:Comprar KVM items.:Confirmar Puntos KVM.:Explicacion de las recompensas KVM.:Explicacion de los puntos KVM.")) {
	case 1:
		mes "[Logistics]";
		mes "Aqui esta el catalogo de los items KVM.";
		mes "Cada arma requiere 2,000 puntos";
		mes "y cada armor desde 10 a 1,200";
		mes "puntos. Asi que, se cuidadoso cuando";
		mes "seleccionas una recompensa.";
		close2;
		readbook 11017,1;
		end;
	case 2:
		mes "[Logistics]";
		mes "Cuales items deseas ver?";
		mes "Buscas detalles especificos de";
		mes "los items, porfavor referirse al";
		mes "^3131FFCatalogo^000000.";
		next;
		switch(select("Weapon:Armor/Accessory:Mass-Production Armor/Accessory:Popularized Armor/Accessory")) {
		case 1:
			mes "[Logistics]";
			mes "Has seleccionado la categoria de armas.";
			mes "Porfavor seleccione una sub-categoria.";
			next;
			switch(select("Dagger/Sword/Spear:Staff/Mace/Axe/Shuriken:Bow/Katar/Instrument/Whip:Book/Knuckle:Revolver/Rifle/Gun/Grenade Launcher")) {
			case 1:
				mes "[Logistics]";
				mes "Has seleccionado la categoria Dagger/Sword/Spear.";
				mes "Porfavor seleccione una sub-categoria";
				next;
				switch(select("Dagger:One-handed Sword:Two-handed Sword:One-handed Spear:Two-handed Spear")) {
				case 1: callsub PurchaseItem,13042,0,1; //Krieger_Dagger1 
				case 2:
					mes "[Logistics]";
					mes "Has seleccionado la categoria 'One-handed Sword'.";
					mes "Ahi 3 One-handed Swords: Glorious Flamberge, Glorious Rapier y Glorious Holy Avenger.";
					mes "Si deseas sus detalles, porfavor refierase al KVM catalogo.";
					mes "Porfavor seleccione uno de ellos.";
					next;
					switch(select("Glorious Flamberge:Glorious Rapier:Glorious Holy Avenger")) {
					case 1: callsub PurchaseItem,13416,0,0; //Krieger_Onehand_Sword1
					case 2: callsub PurchaseItem,13417,0,0; //Krieger_Onehand_Sword2
					case 3: callsub PurchaseItem,13418,0,0; //Krieger_Onehand_Sword3
					}
				case 3: callsub PurchaseItem,1187,0,1; //Krieger_Twohand_Sword1
				case 4: callsub PurchaseItem,1426,0,1; //Krieger_Onehand_Spear1
				case 5: callsub PurchaseItem,1486,0,1; //Krieger_Twohand_Spear1
				}
			case 2:
				mes "[Logistics]";
				mes "Has seleccionado la categoria 'Staff/Mace/Axe/Shuriken'.";
				mes "Porfavor seleccione una categoria.";
				next;
				switch(select("Staff:Mace:One-handed Axe:Two-handed Axe:Shuriken")) {
				case 1:
					mes "[Logistics]";
					mes "Has seleccionado la categoria 'Staff'.";
					mes "Existen 3 Staffs: a Two-handed Weapon Glorious Destruction Staff, a One-handed Glorious Arc Wand y a Glorious Healing Staff.";
					mes "Si quieres mas detalle,porfavor referirse al catalogo KVM.";
					mes "Porfavor seleccione uno de ellos.";
					next;
					switch(select("Glorious Destruction Staff:Glorious Arc Wand:Glorious Healing Staff")) {
					case 1: callsub PurchaseItem,2002,0,0; //Krieger_Twohand_Staff1
					case 2: callsub PurchaseItem,1640,0,0; //Krieger_Onehand_Staff1
					case 3: callsub PurchaseItem,1641,0,0; //Krieger_Onehand_Staff2
					}
				case 2: callsub PurchaseItem,1546,0,1; //Krieger_Onehand_Mace1
				case 3: callsub PurchaseItem,1310,0,1; //Krieger_Onehand_Axe1
				case 4: callsub PurchaseItem,1382,0,1; //Krieger_Twohand_Axe1
				case 5: callsub PurchaseItem,13307,0,1; //Krieger_Huuma_Shuriken1
				}
			case 3:
				mes "[Logistics]";
				mes "Has seleccionado la categoria Bow/Katar/Instrument/Whip.";
				mes "Porfavor seleccione una sub-categoria.";
				next;
				switch(select("Bow:Katar:Instrument:Whip")) {
				case 1: callsub PurchaseItem,1743,0,1; //Krieger_Bow1
				case 2:
					mes "[Logistics]";
					mes "Has seleccionado la categoria 'Katar'.";
					mes "Existen 2 katar: Glorious Bloody Roar y Glorious Jamadhar.";
					mes "Si quieres mas detalle,porfavor referirse al catalogo KVM.";
					mes "Porfavor seleccione uno de ellos.";
					next;
					switch(select("Glorious Bloody Roar:Glorious Jamadhar")) {
					case 1: callsub PurchaseItem,1281,0,0; //Krieger_Katar1
					case 2: callsub PurchaseItem,1282,0,0; //Krieger_Katar2
					}
				case 3: callsub PurchaseItem,1927,0,1; //Krieger_Instrument1
				case 4: callsub PurchaseItem,1981,0,1; //Krieger_Whip1
				}
			case 4:
				mes "[Logistics]";
				mes "Has seleccionado la categoria de Book/Knuckle.";
				mes "Porfavor seleccione una sub-categoria.";
				next;
				switch(select("Book:Knuckle")) {
				case 1:
					mes "[Logistics]";
					mes "Has seleccionado 'Book'.";
					mes "Existen 2 libros: Glorious Tablet y Glorious Apocalypse.";
					mes "Si quieres mas detalle,porfavor referirse al catalogo KVM.";
					mes "Porfavor seleccione uno de ellos.";
					next;
					switch(select("Glorious Tablet:Glorious Apocalypse")) {
					case 1: callsub PurchaseItem,1576,0,0; //Krieger_Book1
					case 2: callsub PurchaseItem,1577,0,0; //Krieger_Book2
					}
				case 2:
					mes "[Logistics]";
					mes "Has seleccionado la categoria 'Knuckle'.";
					mes "Existen 2  Knuckles: Glorious Claw and Glorious Fist.";
					mes "Si quieres mas detalle,porfavor referirse al catalogo KVM.";
					mes "Porfavor seleccione uno de ellos.";
					next;
					switch(select("Glorious Claw:Glorious Fist")) {
					case 1: callsub PurchaseItem,1826,0,0; //Krieger_Knuckle1
					case 2: callsub PurchaseItem,1827,0,0; //Krieger_Knuckle2
					}
				}
			case 5:
				mes "[Logistics]";
				mes "Has seleccionado la categoria 'Revolver/Rifle/Gun/Grenade Launcher'.";
				mes "Porfavor seleccione una sub-categoria.";
				next;
				switch(select("Revolver:Rifle:Gatling Gun:Shotgun:Grenade Launcher")) {
				case 1: callsub PurchaseItem,13110,0,1; //Krieger_Pistol1
				case 2: callsub PurchaseItem,13176,0,1; //Krieger_Rifle1
				case 3: callsub PurchaseItem,13177,0,1; //Krieger_Gatling1
				case 4: callsub PurchaseItem,13178,0,1; //Krieger_Shotgun1
				case 5: callsub PurchaseItem,13179,0,1; //Krieger_Launcher1
				}
			}
		case 2:
			mes "[Logistics]";
			mes "Has seleccionado la categoria de 'Armor/Accessory'.";
			mes "Porfavor seleccione una sub-categoria.";
			next;
			switch(select("Armor:Cloak:Shoes:Accessory")) {
			case 1: callsub PurchaseItem,2394,1,2; //Krieger_Suit1
			case 2: callsub PurchaseItem,2549,2,2; //Krieger_Muffler1
			case 3: callsub PurchaseItem,2444,3,2; //Krieger_Shoes1
			case 4: callsub PurchaseItem,2772,4,2; //Krieger_Ring1
			}
		case 3:
			mes "[Logistics]";
			mes "Has seleccionado la categoria  'Mass-Production Armor/Accessory' category.";
			mes "Porfavor seleccione una sub-categoria.";
			next;
			switch(select("Mass-Production Armor:Mass-Production Shoes:Mass-Production Accessory")) {
			case 1: callsub PurchaseItem,2395,5,3; //Krieger_Suit2
			case 2: callsub PurchaseItem,2445,6,3; //Krieger_Shoes2
			case 3: callsub PurchaseItem,2773,7,3; //Krieger_Ring2
			}
		case 4:
			mes "[Logistics]";
			mes "Has seleccionado la categoria  'Popularized Armor/Accessory'.";
			mes "Porfavor seleccione una sub-categoria.";
			next;
			switch(select("Popularized Armor:Popularized Shoes:Popularized Accessory")) {
			case 1: callsub PurchaseItem,2396,8,4; //Krieger_Suit3
			case 2: callsub PurchaseItem,2446,9,4; //Krieger_Shoes3
			case 3: callsub PurchaseItem,2774,10,4; //Krieger_Ring3
			}
		}
	case 3:
		mes "[Logistics]";
		mes ""+ .@name$ +", Tus puntos actuales son ^580080"+ kvm_point +"^000000 KVM Points.";
		close;
	case 4:
		mes "[Logistics]";
		mes "Existen 3 categorias en las recompensas KVM: Weapon/Armor.Accessory.";
		next;
		mes "[Logistics]";
		mes "Las armas requieren 2,000 KVM puntos cada una.";
		mes "Existen 3 tipos de recompenzas de armor y requieren 840/630/580 KVM puntos cada uno.";
		mes "Las recompensas de accesorios requieren 1,200 KVM puntos cada una.";
		next;
		mes "[Logistics]";
		mes "Kreiger Weapons son armas nivel 4, ellas se le puede subir el nivel y tendran efectos especiales: Slaughter, Destruction and Blessing.";
		next;
		mes "[Logistics]";
		mes "Cuando puedas usar los 3 armos, puedes tener un set especial de opciones.";
		mes "Adem치s, cuando las recompensas Krieger accesorios se llevan con medalism le traer치 recompensas especiales establecidas .";
		next;
		mes "[Logistics]";
		mes "Finalmente , hay producci칩n en masa de armadura y las recompensas de accesorios.";
		mes "Los jugadores cuyos Lvl es superior a 60 se pueden equipar armaduras de producci칩n en massa y existe un level limite para la popularizada armor.";
		close;
	case 5:
		mes "[Logistics]";
		mes "KVM puntos son recompensados cuando ganas, pierdes o empatas en Kreiger Von Midgard (KVM).";
		mes "Puedes tener recompensas especiales con esto.";
		close;
	}

PurchaseItem:
	// Pricing for Weapon, Suit1, Muffler1, Shoes1, Ring1m Suit2, Shoes2, Ring2, Suit3, Shoes3, Ring3
	setarray .@prices[0],2000,840,630,580,1200,120,70,200,15,10,30;

	mes "[Logistics]";

	if (!getarg(2))
		mes "Has seleccionado ^ff0000"+getitemname(getarg(0))+"^000000.";
	else if (getarg(2) == 1)
		mes "Solo hay uno "+callfunc("F_GetWeaponType",getarg(0))+": "+getitemname(getarg(0))+".";
	else if (getarg(2) == 2) {
		mes "Has seleccionado '"+callfunc("F_GetArmorType",getarg(0))+"'.";
		mes "El armor para quien tenga level mayor de 80 es: ^ff0000"+getitemname(getarg(0))+"^000000.";
	}
	else if (getarg(2) == 3) {
		mes "Has seleccionado produccion en masa "+callfunc("F_GetArmorType",getarg(0))+"'.";
		mes "Solo hay una produccion en masa "+callfunc("F_GetArmorType",getarg(0))+": ^ff0000"+getitemname(getarg(0))+"^000000.";
	}
	else if (getarg(2) == 4) {
		mes "Has selecccionado 'Popularized "+callfunc("F_GetArmorType",getarg(0))+"'.";
		mes "Solo existe una Popularized "+callfunc("F_GetArmorType",getarg(0))+": ^ff0000"+getitemname(getarg(0))+"^000000.";
	}

	if (!getarg(1))
		mes "Este requiere ^0000ff2,000^000000 KVM Points, y "+ .@name$ +", Tus puntos son: ^580080"+ kvm_point +"^000000.";
	else if (getarg(1) == 4)
		mes "Este requiere ^0000ff1,200^000000 KVM Points, y "+ .@name$ +", tus puntos son: ^580080"+ kvm_point +"^000000.";
	else
		mes "Este requiere ^0000ff"+.@prices[getarg(1)]+"^000000 KVM Points, y "+ .@name$ +", tus puntos son : ^580080"+ kvm_point +"^000000.";

	mes "Estas seguro que quieres este item?";
	next;
	switch(select("No,No lo comprare.:Si, lo comprare.")) {
	case 1:
		mes "[Logistics]";
		mes "Has seleccionado 'No lo comprare'.";
		mes "Cuando compras un item, ten cuidado no ay devolucion.";
		break;
	case 2:
		if (kvm_point >= .@prices[getarg(1)]) {
			set kvm_point,kvm_point-.@prices[getarg(1)];
			getitem getarg(0),1;
			mes "[Logistics]";
			mes "Has comprado un "+getitemname(getarg(0))+".";
			if (!getarg(1))
				mes "Tus puntos KVM se han reducido a ^0000ff"+getarg(1)+"^000000 puntos, tus puntos KVM ahora son ^580080"+kvm_point+"^000000.";
			if (getarg(1) == 4)
				mes "Tus puntos KVM se han reducido a ^0000ff1,200^000000 puntos, tus puntos KVM ahora son ^580080"+kvm_point+"^000000.";
			else
				mes "Tus puntos KVM se han reducido a ^0000ff"+.@prices[getarg(1)]+"^000000 puntos, tus puntos KVM ahora son ^580080"+kvm_point+"^000000.";
		}
		else {
			mes "[Logistics]";
			if (!getarg(1))
				mes "Tu necesitas ^0000ff2,000^000000 KVM Puntos para comprar este item.";
			else if (getarg(1) == 4)
				mes "Tu necesitas ^0000ff1,200^000000 KVM Puntos para comprar este item.";
			else
				mes "Tu necesitas ^0000ff"+.@prices[getarg(1)]+"^000000 KVM Puntos para comprar este item.";
			mes "Como sea tus puntos KVM son ^580080"+ kvm_point +"^000000.";
			mes "Cuales no son suficientes para comprarlo.";
			mes "Cuando tengas suficientes puntos, regresa otra ves.";
		}
	}
	close;
}
