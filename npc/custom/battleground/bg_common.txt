//===== Hercules Script ======================================
// BattleGround System - Common NPCs
//===== By: ==================================================
//= ????, L0ne_W0lf
//===== Current Version: =====================================
//= 1.4
//===== Description: =========================================
//= [Official Conversion]
//= Battleground NPCs:
//= - Generals and Aides
//= - Battleground Warper
//= - Kafra and Repairman.
//= - GM Management NPC (disabled by default)
//= - Badge Exchanger (Tierra and Flavius)
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Updated several NPCs to Official.
//= 1.2 Updated Repairman NPC
//= 1.3 Optimized "Erundek" NPC. [Euphy]
//= 1.4 Added GM management function. [Euphy]
//============================================================

// Generals
//============================================================
bat_room,161,158,3	script	Gen. Guillaume's Aide#01	4_M_KY_HEAD,{ end; }
bat_room,161,160,3	script	Gen. Guillaume's Aide#03	4_M_KY_HEAD,{ end; }

bat_room,160,141,3	script	Prince Croix	4_M_CRU_CRUA,{
	cutin "bat_crua1",2;
	mes "[Prince Croix]";
	mes "Wise adventurer, why don't you lend us your power for victory?";
	next;
	switch(select("What's the reason for the Battle?:Tell me about General Guillaume")) {
	case 1:
		cutin "bat_crua2",2;
		mes "[Prince Croix]";
		mes "Maroll's great king, Marcel Marollo VII, is very sick lately.";
		mes "His Majesty has declared that he will be leaving the future of Maroll to me or the 3rd prince, General Guillaume.";
		next;
		mes "[Prince Croix]";
		mes "General Guillaume may have an advantage in this battle as he is the great general of Maroll, but that doesn't automatically mean he'll win.";
		mes "I want to win this battle so that I can bring prosperity to the people of Maroll. They've suffered enough from war...";
		next;
		switch(select("Yes, I want to join you.:End Conversation")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Prince Croix]";
			mes "Thank you so much. I feel like I can win with the help of adventurers like you.";
			mes "Now, please go downstairs and join your comrades in sharpening their skills to fight the enemy!";
			break;
		case 2:
			mes "[Prince Croix]";
			mes "For Maroll!";
			break;
		}
		break;
	case 2:
		cutin "bat_crua2",2;
		mes "[Prince Croix]";
		mes "The 3rd Prince Guillaume is the great general of Maroll.";
		mes "It's a waste of time to explain to you how great a leader or warlord he is, since he commands the great military power of Maroll.";
		next;
		mes "[Prince Croix]";
		mes "Unfortunately, there's something he and his followers are unaware of:";
		mes "Do the people of Maroll really want them to spend so much money on military power?";
		mes "We have suffered enough from wars.";
		mes "I believe weapons aren't the best way to bring prosperity to a nation.";
		next;
		mes "[Prince Croix]";
		mes "I do not wish to shed blood, but I have no choice but to fight for the possibility of peace and for the sake of my people.";
		next;
		switch(select("Yes, I want to join you.:End Conversation")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Prince Croix]";
			mes "Thank you so much. I feel like I can win with the help of adventurers like you.";
			mes "Now, please go downstairs and join your comrades in sharpening their skills to fight the enemy!";
			break;
		case 2:
			mes "[Prince Croix]";
			mes "For Maroll!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_crua1",255;
	cutin "bat_crua2",255;
	end;
}

bat_room,161,140,3	script	Prince Croix's Aide#01	4_M_CRU_HEAD,{ end; }
bat_room,161,142,3	script	Prince Croix's Aide#02	4_M_CRU_HEAD,{ end; }

bat_room,160,159,3	script	General Guillaume	4_M_KY_KIYOM,{
	cutin "bat_kiyom2",2;
	mes "[General Guillaume]";
	mes "Hot-blooded adventurer, we need your ability to win this battle.";
	next;
	switch(select("What's the reason for the Battle?:Tell me about Prince Croix")) {
	case 1:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "Our great king, Marcel Marollo VII, is very sick lately.";
		mes "His Majesty has declared that he has chosen either me or Prince Croix as the next king amongst his 9 sons.";
		next;
		mes "[General Guillaume]";
		mes "Two kings can't share a nation! Only the one victorious from His Majesty's appointed battle will be enthroned.";
		next;
		mes "[General Guillaume]";
		mes "This is, however, not just a battle between us. This battle will determine the future of this country.";
		mes "I pledge on my honor to prove that I'm the one who can protect this Maroll from outside threats.";
		next;
		switch(select("Yes, I want to join you.:End Conversation")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "Welcome to my army, comrade.";
			mes "Your eyes tell me that you're a soldier that I can trust.";
			next;
			mes "[General Guillaume]";
			mes "Now, go upstairs and apply for battle with your comrades.";
			mes "I'm sure they'll welcome you whole-heartedly!";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "I'll be the one who will capture the flag!";
			break;
		}
		break;
	case 2:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "The 5th Prince Croix is currently titled as the Prime Minister of Maroll.";
		mes "He thinks all national matters of a nation can be discussed and determined on a desk,";
		mes "and believes in peaceful co-existence with other countries.";
		next;
		mes "[General Guillaume]";
		mes "He's too ignorant to admit that so-called peace is built on countless lives that are sacrificed in wars while normal citizens and upper classes can live, oblivious to the horrors that allow them to live that way.";
		next;
		mes "[General Guillaume]";
		mes "He's too naive to understand the reality....";
		mes "I can't leave Maroll to someone like him who lives in a dream!";
		next;
		mes "[General Guillaume]";
		mes "His unrealistic beliefs will drown this country in poverty and make the people weak. If he becomes the king, Maroll will never rest from the onslaughts of other countries.";
		mes "I want to teach him what makes this small country so powerful and prosperous. It's military power!";
		next;
		switch(select("I want to join your army!:End Conversation")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "Welcome to my army, comrade.";
			mes "Your eyes tell me that you're a soldier that I can trust.";
			next;
			mes "[General Guillaume]";
			mes "Now, go upstairs and apply for battle from your comrades.";
			mes "I'm sure they'll welcome you whole-heartedly!";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "I'll be the one who will capture the flag!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_kiyom1",255;
	cutin "bat_kiyom2",255;
	end;
}


// Repairman
//============================================================
pvp_castle,80,97,5	script	Repairman#bg	4_M_04,{
	callfunc "repairmain","Repairman";
	end;
}


// Badges Exchange
//============================================================
pvp_castle,85,92,5	script	Erundek	4_M_MANAGER,{
	if (checkweight(1201,1) == 0) {
		mes "- Espera un minuto !! -";
		mes "- Ahora mismo estas cargando -";
		mes "- Demasiados items. -";
		mes "- Porfavor intentalo de nuevo -";
		mes "- Despues de perder algo de peso. -";
		close;
	}
	mes "[Erundek]";
	mes "Posees medallas del campo de batalla?";
	mes "Yo puedo cambiarte Bravery Badges y Valor Badges por items de recompensa.";
	next;
	switch(select("Cambiar medallas:Checear catalogo")) {
	case 1:
		mes "[Erundek]";
		mes "Que tipo de items deseas cambiar?";
		mes "Para checear mas informacion de nuestros items de recompensa, porfavor vea nuestro ^3131FFCatalogo^000000.";
		next;
		switch(select("Weapon:Armor:Accessory:Consumable")) {
		case 1:
			mes "[Erundek]";
			mes "Has escojido ^3131FFArma^000000.";
			mes "Las sigientes armas estan disponibles para cambiarse por medallas del campo de batalla.";
			mes "Porfavor note que los items ^3131FFBravery Badges estan indicados como (BB)^000000, y ^3131FFValor Badges como (VB)^000000.";
			next;
			switch(select("Dagger/OneSword/TwoSword/TwoSpear:Staff/Mace/TwoAxe/Shuriken:Bow/Katar/Music/Whip:Book/Knuckle:Revolver/Rifle/Gatling/Shotgun/Launcher")) {
			case 1:
				mes "[Erundek]";
				mes "Los sigientes items estan disponibles en ^3131FFDagger, One-Handed Sword, Two-Handed Sword, y Two-Handed Spear^000000 categoria.";
				next;
				setarray .@Weapons[0],13036,7828,13037,7829,13411,7828,13410,7829,1183,7828,1184,7829,1425,7828,1482,7829;
				break;
			case 2:
				mes "[Erundek]";
				mes "Los sigientes items estan disponibles en ^3131FFStaff / Mace / Two-Handed Axe / Huuma Shuriken^000000 categoria.";
				next;
				setarray .@Weapons[0],1632,7828,1633,7829,1634,7828,1635,7829,1543,7828,1542,7829,1380,7828,1379,7829,13305,7828,13306,7829;
				break;
			case 3:
				mes "[Erundek]";
				mes "Los sigientes items estan disponibles en ^3131FFBow / Katar / Musical Instrument / Whip^000000 categoria.";
				next;
				setarray .@Weapons[0],1739,7828,1738,7829,1279,7828,1280,7829,1924,7828,1923,7829,1978,7828,1977,7829;
				break;
			case 4:
				mes "[Erundek]";
				mes "Los sigientes items estan disponibles en ^3131FFBook / Knuckle^000000 categoria.";
				next;
				setarray .@Weapons[0],1574,7828,1575,7829,1824,7828,1823,7829;
				break;
			case 5:
				mes "[Erundek]";
				mes "Los sigientes items estan disponibles en ^3131FFRevolver / Rifle / Gatling Gun / Shotgun / Grenade Launcher^000000 categoria.";
				next;
				setarray .@Weapons[0],13108,7828,13171,7829,13172,7828,13173,7829,13174,7829;
				break;
			}
			set .@menu$,"";
			for(set .@i,0; .@i<getarraysize(.@Weapons); set .@i,.@i+2)
				set .@menu$, .@menu$+getitemname(.@Weapons[.@i])+((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)")+":";
			set .@i, (select(.@menu$)-1)*2;
			set .@type$, ((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)");
			mes "[Erundek]";
			mes "Has Escojido ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000.";
			mes "Tu puedes cambiar este item con ^FF0000100 "+getitemname(.@Weapons[.@i+1])+"^000000.";
			mes "Te gustaria cambiarlo?";
			next;
			switch(select("Do not exchange:Exchange")) {
			case 1:
				break;
			case 2:
				mes "[Erundek]";
				mes "Te gustaria gastar ^FF0000100 "+getitemname(.@Weapons[.@i+1])+"^000000 and receive a ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000?";
				next;
				mes "[Erundek]";
				mes "Recuerda, Los premios de recompensa del campo de batalla son ^FF0000Unicas por Personaje^000000. Estas seguro que quieres este item?";
				next;
				switch(select("Si:No")) {
				case 1:
					mes "[Erundek]";
					if (countitem(.@Weapons[.@i+1]) >= 100) {
						mes "Gracias por Cambiar.";
						delitem .@Weapons[.@i+1],100;
						getitem .@Weapons[.@i],1;
					}
					else mes "Lo siento, pero no tienes suficientes medallas.";
					close;
				case 2:
					break;
				}
				break;
			}
			mes "[Erundek]";
			mes "Necesitas mas tiempo para chequear los items?";
			close;
		case 2:
			mes "[Erundek]";
			mes "Has escojido ^3131FFArmor^000000.";
			mes "Las sigientes armaduras estan disponibles para cambiar por medallas del campo de batalla.";
			next;
			switch(select("Garments / Shoes:Armor")) {
			case 1:
				setarray .@items[0],2538,50,2539,50,2540,50,2435,50,2436,50,2437,50;
				break;
			case 2:
				setarray .@items[0],2376,80,2377,80,2378,80,2379,80,2380,80,2381,80,2382,80;
				break;
			}
			break;
		case 3:
			mes "[Erundek]";
			mes "Has escojido ^3131FFAccessory^000000.";
			mes "Tu puedes cambiar tus medallas de honor por medallas dependiendo de tu clase de trabajo, como la sigiente:";
			next;
			setarray .@items[0],2733,500,2720,500,2721,500,2722,500,2723,500,2724,500,2725,500;
			set .@menu1$,"Gunslinger:Swordman/Taekwon Master:Thief:Acolyte:Magician:Archer:Merchant";
			break;
		case 4:
			mes "[Erundek]";
			mes "Has escojido ^3131FFConsumable^000000.";
			mes "Los sigientes items consumibles estan disponibles para cambio por medallas de campo de batalla:";
			next;
			setarray .@items[0],12269,10,12270,10,12271,5,12272,10,12273,10;
			break;
		}
		break;
	case 2:
		mes "[Erundek]";
		mes "Nosotros tenemos muchos items, asi que heche un vistazo y compre deliberadamente.";
		close2;
		readbook 11010,1;
		end;
	}
	set .@menu$,"";
	if (.@menu1$ != "") set .@menu$, .@menu1$;
	else for(set .@i,0; .@i<getarraysize(.@items); set .@i,.@i+2)
		set .@menu$, .@menu$+getitemname(.@items[.@i])+":";
	set .@i, (select(.@menu$)-1)*2;
	mes "[Erundek]";
	mes "Has escojido ^3131FF"+getitemname(.@items[.@i])+"^000000.";
	switch(.@items[.@i]) {
		case 2720: mes "Este item es solamente para Swordman y Taekwon Master Class solamente."; break;
		case 2721: mes "Este item es solamente para Thief Class solamente."; break;
		case 2722: mes "Este item es solamente para Acolyte Class solamente."; break;
		case 2723: mes "Este item es solamente para Magician Class solamente."; break;
		case 2724: mes "Este item es solamente para Archer Class solamente."; break;
		case 2725: mes "Este item es solamente para Merchant Class solamente."; break;
		case 2733: mes "Este item es solamente para Gunslinger solamente."; break;
		default: break;
	}
	mes "Tu puedes cambiar este item por ^FF0000"+.@items[.@i+1]+" "+getitemname(7828)+" O "+.@items[.@i+1]+" "+getitemname(7829)+"^000000.";
	mes "Te gustaria cambiarlo?";
	next;
	switch(select("No Cambiar:Cambiar")) {
	case 1:
		mes "[Erundek]";
		mes "Necesitas mas tiempo para chequear los items?";
		break;
	case 2:
		mes "[Erundek]";
		mes "Que medalla deseas cambiar?";
		mes "Necesitas ^3131FF"+.@items[.@i+1]+" Medallas^000000 para cambiar.";
		next;
		if (.@item[0] < 12269 || .@item[0] > 12273 ) {
			mes "[Erundek]";
			mes "Recuerda, Las recompensas del campo de batalla son ^FF0000Unicas por Personaje^000000. Estas seguro que quieres este item?";
			next;
		}
		set .@j, select("Bravery Badge:Valor Badge:Cancel");
		mes "[Erundek]";
		if (.@j == 3) {
			mes "Has cancelado el cambio.";
			break;
		}
		set .@cost, ((.@j==1)?7828:7829);
		if (countitem(.@cost) >= .@items[.@i+1]) {
			mes "Gracias por cambiar.";
			delitem .@cost, .@items[.@i+1];
			getitem .@items[.@i],1;
		}
		else mes "No tienes suficiente "+getitemname(.@cost)+"s.";
		break;
	}
	close;
}

// PVP Point Trader
pvp_castle,111,101,4	script	PVP Point Trader::pvp_point_trader	470,{
	function trade;

	mes "^006633[ PVP Point Trader ]^000000";
	if (checkweight(1201,1) == 0) {
		mes "- Espera un minuto !";
		mes "- Actualmente estas llevando  -";
		mes "- demaciados items contigo. -";
		mes "- Por favor intenta de nuevo -";
		mes "- despues de perder algo de peso.";
		close;
	}
	mes "Yo tradeo armas por puntos que has ganado en los modos de ^0000ffPVP Hunt^000000 y ^0000ffPVP KoE^000000 en esta sala.";
	mes " ";
	if(#PVPPOINTS < .PRICE) {
		mes "Pero no tradeo ningun arma por menos de ^ff0000"+.PRICE+" Puntos de PVP^000000";
		close;
		end;
	}

	mes "Que deseas tradear de mi lista?";
	setarray .@m$, "Dagger","One Handed Sword","Two Handed Sword","Axe","Mace","Bow","Staff","Book","Spear","Katar","Knuckle","Whip","Musical Instrument";
		set .@m, select (implode(.@m$,":")) - 1;
		switch (.@m) {
			case 0:
				setarray .@mw$, "Ginnungagap:Grimtooth:Dragon Killer:Mail Breaker:Bazerald:Sword Breaker:Ice Pick:Sucsamad:Kitchen Knife:Azoth:Exorciser:Assassin Dagger:Moonlight Dagger:Weeder Knife:Cursed Dagger:Dagger of Counter:Combat Knife:Fortune Sword";
				setarray .@mw, 13002,1237,13001,1225,1231,1224,1230,1236,1229,1235,1233,1232,1234,1227,1241,1242,1228,1223;
				break;
			case 1:
				setarray .@mw$, "Nagan:Immaterial Sword:Mysteltainn:Byeollungum:Star Dust Blade:Caesar's Sword:Ice Falchion:Excalibur:Edge:Cutlus:Solar Sword:Tirfing:Fireblend";
				setarray .@mw, 1130,1141,1138,1140,1148,1134,1131,1137,1132,1135,1136,1139,1133;
				break;
			case 2:	
				setarray .@mw$, "Dragon Slayer:Masamune:Muramasa:Schweizersabel:Executioner:Zweihander:Katzbalger";
				setarray .@mw, 1166,1165,1164,1167,1169,1168,1170;
				break;
			case 3:
				setarray .@mw$, "Great Axe:Guillotine:Light Epsilon:Bloody Axe:Sabbath:Slaughter:Cleaver:Tomahawk";
				setarray .@mw, 1364,1369,1366,1363,1365,1367,1305,1368;
				break;
			case 4:
				setarray .@mw$, "Golden Mace:Grand Cross:Long Mace:Spike:Slash:Quadrille";
				setarray .@mw, 1524,1528,1525,1523,1526,1527;
				break;
			case 5:
				setarray .@mw$, "Roguemaster's Bow:Dragon Wing:Rudra's Bow:Ballista";
				setarray .@mw, 1719,1724,1720,1722;
				break;
			case 6:
				setarray .@mw$, "Wing Staff:Wizardry Staff";
				setarray .@mw, 1616,1473;
			case 7:
				setarray .@mw$, "Legacy of Dragon:Book of the Apocalypse:Girl's Diary:Hardcover Book";
				setarray .@mw, 1559,1557,1558,1561;
				break;
			case 8:
				setarray .@mw$, "Gae Bolg:Gelerdria:Gungnir:Skewer:Longinus's Spear:Brionac:Bill Guisarme:Zephyrus:Crescent Scythe:Tjungkuletti:Hellfire";
				setarray .@mw, 1474,1414,1413,1415,1469,1470,1467,1468,1466,1416,1471;
				break;
			case 9:
				setarray .@mw$, "Infiltrator:Bloody Roar:Unholy Touch";
				setarray .@mw, 1261,1265,1263;
				break;
			case 10:
				setarray .@mw$, "Hatii Claw:Berserk:Kaiser Knuckle";
				setarray .@mw, 1815,1814,1813;
				break;
			case 11:
				setarray .@mw$, "Lariat:Rapture Rose:Blade Whip:Chemeti:Queen's Whip";
				setarray .@mw, 1962,1963,1969,1964,1970;
				break;
			case 12:
				setarray .@mw$, "Oriental Lute:Electric Guitar";
				setarray .@mw, 1918,1913;
				break;		
		}
		set .@j, select (.@mw$+":Cancelar") - 1;
		explode(.@mw$,.@mw$,":");
		if (.@j == getarraysize(.@mw$)) close;
		trade(.@mw[.@j]);
		//callsub S_BonusReward,1500,.@mw[.@j];
		end;

	function trade {
		set #PVPPOINTS,#PVPPOINTS-.PRICE;
		getitem getarg(0),1;
		next2;
		mes "^006633[ PVP Point Trader ]^000000";
		mes "Exelente negocio !";
		mes "Regresa siempre que quieras";
		close;
	}

	OnInit:
		set .PRICE,10000; //Cantidad de Puntos Necesarios para cambiar items
	end;
}

pvp_castle,114,97,4	script	PVP Points Checker::pvp_points_check	654,{
	mes "^cc66cc[ PVP Point Checker ]^000000";
	mes "Jugando ^0000ffPVP Hunt^000000 O ^0000ffPVP KoE^000000 obtienes un puntaje especial llamado";
	mes "^ff0000Puntos de PVP^000000";
	mes "Actualmente tienes un total de: ";
	mes "^808080"+#PVPPOINTS+" Puntos de PVP^000000";
	close;
}